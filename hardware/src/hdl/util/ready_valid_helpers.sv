`timescale 1ns / 1ps

/**
 * The ReadyValidDuplicator creates NUM_OUTPUTS outputs based on one input. The ready signal of 
 * the input is driven when all output ready signals are high.
 */
module ReadyValidDuplicator #(
    parameter integer NUM_OUTPUTS
) (
    input logic clk,
    input logic rst_n,

    ready_valid_i.s in,                 // #(data_t)
    ready_valid_i.m out[NUM_OUTPUTS] // #(data_t)
);

logic[NUM_OUTPUTS - 1:0] out_ready;
logic[NUM_OUTPUTS - 1:0] seen, n_seen;

assign in.ready = &(seen | out_ready);

always_ff @(posedge clk) begin
    if(!rst_n) begin
        seen <= '0;     
    end else begin
        seen <= n_seen;
    end
end

always_comb begin
    n_seen = seen;

    if (in.ready) begin
        n_seen = '0;
    end else if (in.valid) begin
        n_seen = seen | out_ready;
    end
end

for (genvar I = 0; I < NUM_OUTPUTS; I++) begin
    assign out_ready[I] = out[I].ready;

    assign out[I].data  = in.data;
    assign out[I].valid = in.valid && !seen[I];
end

endmodule

/**
 * A registered version of the ReadyValidDuplicator with at least 2 clock cycles of latency.
 * The registered version breaks the ready-valid chain in chained ready-valid modules.
 */
module RegisteredReadyValidDuplicator #(
    parameter type data_t,
    parameter integer NUM_OUTPUTS
) (
    input logic clk,
    input logic rst_n,

    ready_valid_i.s in,              // #(data_t)
    ready_valid_i.m out[NUM_OUTPUTS] // #(data_t)
);

ready_valid_i #(data_t) _in (.clk (clk), .rst_n (rst_n));
SkidBuffer #(data_t) inst_skid_buf (
    .clk (clk),
    .rst_n (rst_n),

    .in (in),
    .out (_in)
);

ReadyValidDuplicator #(NUM_OUTPUTS) inst_duplicator (
    .clk (clk),
    .rst_n (rst_n),

    .in (_in),
    .out (out)
);

endmodule

/**
 * The ReadyValidCombiner combines the data of two inputs into one output.
 */
module ReadyValidCombiner (
    ready_valid_i.s left,  // #(left_t)
    ready_valid_i.s right, // #(right_t)
    ready_valid_i.m out    // #({left_t, right_t})
);

assign left.ready  = right.valid && out.ready;
assign right.ready = left.valid  && out.ready;

assign out.data  = {left.data, right.data};
assign out.valid = left.valid && right.valid;

endmodule

/**
 * The ReadyValidSplitter splits one input into two outputs based on the given parameter types. The 
 * ready signal of the input is driven after both output ready signals have been high.
 */
module ReadyValidSplitter #(
    parameter type left_t,
    parameter type right_t
) (
    input logic clk,
    input logic rst_n,

    ready_valid_i.s in,   // #({left_t, right_t})
    ready_valid_i.m left, // #(left_t)
    ready_valid_i.m right // #(right_t)
);

typedef struct packed {
    left_t left;
    right_t right;
} combined_t;

combined_t combined_data;

logic[1:0] out_ready;
logic[1:0] seen, n_seen;

assign in.ready = &(seen | out_ready);

always_ff @(posedge clk) begin
    if(!rst_n) begin
        seen <= '0;     
    end else begin
        seen <= n_seen;
    end
end

always_comb begin
    n_seen = seen;

    if (in.ready) begin
        n_seen = '0;
    end else if (in.valid) begin
        n_seen = seen | out_ready;
    end
end

assign out_ready[0] = left.ready;
assign out_ready[1] = right.ready;

assign combined_data = in.data;

assign left.data  = combined_data.left;
assign left.valid = in.valid && !seen[0];

assign right.data  = combined_data.right;
assign right.valid = in.valid && !seen[1];

endmodule

module ReadyValidShiftRegister #(
    parameter type data_t,
    parameter DEPTH
) (
    input logic clk,
    input logic rst_n,

    ready_valid_i.s in, // #(data_t)
    ready_valid_i.m out // #(data_t)
);

data_t[DEPTH:0] shift_data;
logic [DEPTH:0] shift_valid;

assign in.ready = out.ready || !shift_valid[1];

assign shift_data[0]  = in.data;
assign shift_valid[0] = in.valid;

always_ff @(posedge clk) begin
    if(!rst_n) begin
        shift_valid[DEPTH:1] <= '0;
    end else begin
        for (int i = 1; i < DEPTH + 1; i++) begin
            if (out.ready || !shift_valid[i]) begin
                shift_data[i]  <= shift_data[i - 1];
                shift_valid[i] <= shift_valid[i - 1];
            end
        end
    end
end

assign out.data  = shift_data[DEPTH];
assign out.valid = shift_valid[DEPTH];

endmodule
